# This file is part of pound testsuite. -*- autotest -*-
# Copyright (C) 2025 Sergey Poznyakoff
#
# Pound is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# Pound is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with pound.  If not, see <http://www.gnu.org/licenses/>.
AT_SETUP([Lua path])
AT_KEYWORDS([cond lua lua-path])

AT_SKIP_IF([test "$LUA_AVAILABLE" != 1])

AT_DATA([path0.lua],
[pound.log(pound.DEBUG, "PATH=" .. package.path)
])

PT_CHECK([
Lua
	LoadGlobal "path0.lua"
End
ListenHTTP
	Service
		Backend
		End
	End
End
])

AT_CHECK([sed -n -e '/^pound: PATH=/{' -e 's///p' -e 'q' -e '}' pound.log],
[0],
[stdout])

m4_pushdef([PATH_PREPEND],[/one/?.lua;/two/?/lua])
m4_pushdef([PATH_PREPEND_Q],[m4_bpatsubst(PATH_PREPEND, [[/.]], [\\\&])])

PT_CHECK([
Lua
	Path	"PATH_PREPEND"
	LoadGlobal "path0.lua"
End
ListenHTTP
	Service
		Backend
		End
	End
End
])

AT_CHECK([sed -n -e '/^pound: PATH=/{' -e 's///' \
                 -e 's/^PATH_PREPEND_Q;//' \
		 -e p \
		 -e 'q' \
		 -e '}' pound.log | cmp - stdout])

m4_popdef([PATH_PREPEND_Q])
m4_popdef([PATH_PREPEND])

AT_CLEANUP